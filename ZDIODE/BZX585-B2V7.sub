*******************************************
*
*BZX585-B2V7
*
*NXP Semiconductors
*
*Voltage regulator diode
*
*VFmax = 1,1V  @ IF = 100mA
*IRmax = 20µA  @ VR = 1V
*VZmax = 2,75V @ IZ = 5mA
*
*Package: SOD523
*Package Pin 1: Cathode 
*Package Pin 2: Anode
*
*Extraction date (week/year): 51/2015
*Simulator: SPICE2
*
******************************************
* CAUTION:
* Due KiCAD definition of schematic symbol,
* the physical pins need to be flipped in
* simulation to fit the SPICE logic:
* Anode first, then Cathode ...
* 
* Original: .SUBCKT BZX585-B2V7 1 2
* 
* Changed by GMVT, mja
******************************************
.SUBCKT BZX585-B2V7 2 1
 R1 1 2 1E+008   
 D1 1 2 DIODE1
 D2 3 1 DIODE2
 VZ 2 3 3E-006   
*
*The resistor R1, the diode D2    
*and VZ do not reflect  
*physical devices but improve 
*only modeling in the reverse 
*mode of operation.
*
 .MODEL DIODE1 D 
 + IS = 1.3E-011 
 + N = 1.44      
 + BV = 3
 + IBV = 0.005   
 + RS = 0.08     
 + CJO = 3.383E-010      
 + VJ = 1
 + M = 0.4       
 + FC = 0.5      
 + TT = 0
 + EG = 1.1      
 + XTI = 3       
 .MODEL DIODE2 D 
 + IS = 8E-011   
 + N = 5 
 + RS = 80       
 .ENDS                
*
